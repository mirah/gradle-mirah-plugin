//buildscript {
//    repositories {
//        maven {
//            url "https://plugins.gradle.org/m2/"
//        }
//        jcenter()
//    }
//    dependencies {
//        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.12.1'
//        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:2.2.2"
//        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
//        classpath "org.ysb33r.gradle:gradletest:1.0-beta3"
//    }
//}
//
//apply plugin: 'groovy'
//apply plugin: 'com.github.hierynomus.license'
//apply plugin : 'org.ysb33r.gradletest'
//apply plugin : 'com.gradle.plugin-publish'

plugins {
    id 'com.gradle.build-scan' version '1.0'
    id 'groovy'
//    id 'org.ysb33r.bintray' version '1.5'
    id 'com.gradle.plugin-publish' version '0.9.4'
    id 'com.github.hierynomus.license' version '0.13.1'
    id 'org.ysb33r.gradletest' version '1.0-beta3'
}

group = 'org.ysb33r.gradle'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.7
plugins.withType(JavaPlugin) {
    project.tasks.withType(GroovyCompile) { task ->
        task.sourceCompatibility = project.sourceCompatibility
        task.targetCompatibility = project.targetCompatibility
    }
    project.tasks.withType(JavaCompile) { task ->
        task.sourceCompatibility = project.sourceCompatibility
        task.targetCompatibility = project.targetCompatibility
    }
}

repositories {
    jcenter()
}

ext {
    spockGroovyVer = GroovySystem.version.replaceAll(/\.\d+$/,'')
    mirahTestVer = '0.1.4'
    mirahTestRepo = "${buildDir}/test/mirahTestRepo"
}

configurations {
  mirahTesting
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'commons-io:commons-io:2.5'
    testCompile ("org.spockframework:spock-core:1.0-groovy-${spockGroovyVer}") {
        exclude module : 'groovy-all'
    }

    mirahTesting "org.mirah:mirah:${mirahTestVer}"
    gradleTest "org.mirah:mirah:${mirahTestVer}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    description "An archive of the JavaDocs for Maven Central"
    classifier "javadoc"
    from javadoc
}

task prepareTestRepo( type : Copy ) {
    from configurations.mirahTesting
    into mirahTestRepo
}

test {
    dependsOn prepareTestRepo

    systemProperties TESTREPO : file(mirahTestRepo).absolutePath,
        TESTROOT : file("${buildDir}/test/mirah").absolutePath,
        TESTRESOURCES : file('src/test/resources/testcode').absolutePath

}

gradleTest {
    versions '2.0' ,'2.2', '2.3', '2.5', '2.8', '2.9', '2.10', '2.12', '2.14'
}

license {
    header = rootProject.file('config/apache-header')
    strictCheck = true
    ignoreFailures = false
    mapping {
        groovy ='SLASHSTAR_STYLE'
        mirah = 'SCRIPT_STYLE'
    }
    ext.year = '2015-2016'
    excludes(['**/*.ad', '**/*.asciidoc', '**/*.adoc', '**/*.md','**/*.properties', '**/*.txt', '**/*.bz2'])
}

artifacts {
    archives sourcesJar, javadocJar
}

pluginBundle {
    description = 'Mirah language plugin for Gradle'
    website     = 'https://github.com/ysb33r/mirah-gradle-plugin'
    vcsUrl      = 'https://github.com/ysb33r/mirah-gradle-plugin.git'

    tags = ['language','mirah']

    plugins {
        mirahBasePlugin {
            id = 'org.mirah.lang.base'
            displayName = 'Mirah language base plugin'
        }
        mirahPlugin {
            id = 'org.mirah.lang'
            displayName = 'Mirah language plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = archivesBaseName
    }

}

publishPlugins {
    onlyIf { !version.endsWith("SNAPSHOT") }
}
